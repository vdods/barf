###############################################################################
# BARF (Bison Awesomely Replaced, Flex) -- a suite of sweet compiler tools.
###############################################################################

cmake_minimum_required(VERSION 3.10)
project(barf)
# The VERSION argument of cmake's project function is not semver compliant.
# See https://gitlab.kitware.com/cmake/cmake/issues/16716
set(PROJECT_VERSION "2.0.0-dev")

set(CMAKE_CXX_STANDARD 11)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# This includes the reflex and trison source file rules (for generating C++).
# Other projects that want to define cmake rules for compiling reflex or trison
# source to C++ should copy the barf/cmake dir into their project and include it.
include(./cmake/reflex-rules.cmake)
include(./cmake/trison-rules.cmake)
# These define developer-specific rules only for use within the barf project itself.
include(./cmake-private/reflex-dev-rules.cmake)
include(./cmake-private/trison-dev-rules.cmake)

# This is the root dir for files to be installed (notably the barf target files)
# This particular value is used as a #define to the various libs and executables
# so they know where the targets dir is.
set(HARDCODED_BARF_TARGETS_DIR ${CMAKE_INSTALL_PREFIX}/lib/barf/targets)

# This indicates all executables should be put in the bin subdir of the build dir.
# Normally this wouldn't be done, but there's a stupid bug in find_program, where
# it searches the current directory for executables, instead of only searching
# the path like a reasonable person would.
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/bin)

# This property is set so that the cpp and hpp files generated by reflex and
# trison aren't cleaned during `make clean`.  This is because those files
# should be checked into the source repo and stay there, only being re-generated
# if the corresponding reflex or trison source (or other source files) changes.
set_directory_properties(PROPERTIES CLEAN_NO_CUSTOM true)

option(barf_DEVELOPER_MODE "Enable (and require) barf developer configuration options; requires having a stable version of barf installed already, which will be used to build the barf parsers and scanners in a self-hosting manner.  Not necessary to simply build this package from source." OFF)
if(barf_DEVELOPER_MODE)
    find_package(barf REQUIRED)
endif()

# This sets the DOXYGEN_EXECUTABLE and DOXYGEN_DOT_EXECUTABLE vars.
find_package(Doxygen REQUIRED dot)
# TODO: Hook up doxygen config and generation

# Options to correctly link the standard C++ lib on Mac.
if(${CMAKE_SYSTEM_NAME} MATCHES "Darwin") # This is the correct way to detect Mac OS X operating system -- see http://www.openguru.com/2009/04/cmake-detecting-platformoperating.html
    set(CMAKE_XCODE_ATTRIBUTE_CLANG_CXX_LIBRARY "libc++")
    if(${CMAKE_CXX_COMPILER_ID} MATCHES "Clang") # GCC ("GNU") probably would require a different option
        set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -stdlib=libc++")
    endif()
endif()

###############################################################################
# Doxygen doc rules
###############################################################################

set(DOXYGEN_OUTPUT_DIRECTORY doc)
doxygen_add_docs(doc ${PROJECT_SOURCE_DIR}/doc/doxygen_pages)

###############################################################################
# Dependencies
###############################################################################

# Helper target(s)

add_library(Strict INTERFACE)
if(${CMAKE_SYSTEM_NAME} MATCHES "Darwin" OR ${CMAKE_SYSTEM_NAME} MATCHES "Linux")
    target_compile_options(Strict INTERFACE -Wall -Werror)
endif()

add_library(SaveTemps INTERFACE)
if(${CMAKE_SYSTEM_NAME} MATCHES "Darwin" OR ${CMAKE_SYSTEM_NAME} MATCHES "Linux")
    target_compile_options(SaveTemps INTERFACE -save-temps)
elseif(${CMAKE_SYSTEM_NAME} MATCHES "Windows")
    # TODO
endif()

###############################################################################
# Libraries
###############################################################################

#
# barf_commonlang_and_targetspec
#

if(barf_DEVELOPER_MODE)
    reflex_add_source(${PROJECT_SOURCE_DIR}/lib/commonlang/barf_commonlang_scanner.reflex barf_commonlang_scanner)
    trison_add_source(${PROJECT_SOURCE_DIR}/lib/targetspec/barf_targetspec_parser.trison barf_targetspec_parser)
endif()

reflex_add_source_dev(${PROJECT_SOURCE_DIR}/lib/commonlang/barf_commonlang_scanner.reflex dev_barf_commonlang_scanner)
trison_add_source_dev(${PROJECT_SOURCE_DIR}/lib/targetspec/barf_targetspec_parser.trison dev_barf_targetspec_parser)

set(barf_commonlang_and_targetspec_SOURCES
    lib/commonlang/barf_commonlang.hpp
    lib/commonlang/barf_commonlang_ast.cpp
    lib/commonlang/barf_commonlang_ast.hpp
    lib/commonlang/barf_commonlang_scanner.cpp
    lib/commonlang/barf_commonlang_scanner.hpp
    lib/commonlang/barf_commonlang_scanner.reflex
    lib/targetspec/barf_targetspec.hpp
    lib/targetspec/barf_targetspec_ast.cpp
    lib/targetspec/barf_targetspec_ast.hpp
    lib/targetspec/barf_targetspec_parser.cpp
    lib/targetspec/barf_targetspec_parser.hpp
    lib/targetspec/barf_targetspec_parser.trison
)

add_library(barf_commonlang_and_targetspec ${barf_commonlang_and_targetspec_SOURCES})
target_compile_definitions(barf_commonlang_and_targetspec PUBLIC PACKAGE_VERSION="${PROJECT_VERSION}" HARDCODED_BARF_TARGETS_DIR="${HARDCODED_BARF_TARGETS_DIR}")
target_include_directories(barf_commonlang_and_targetspec PUBLIC ${PROJECT_SOURCE_DIR}/lib/commonlang)
target_include_directories(barf_commonlang_and_targetspec PUBLIC ${PROJECT_SOURCE_DIR}/lib/targetspec)
target_link_libraries(barf_commonlang_and_targetspec PUBLIC Strict barf_core barf_preprocessor)

#
# barf_core
#

set(barf_core_SOURCES
    lib/core/barf.cpp
    lib/core/barf.hpp
    lib/core/barf_ast.cpp
    lib/core/barf_ast.hpp
    lib/core/barf_commandlineparser.cpp
    lib/core/barf_commandlineparser.hpp
    lib/core/barf_compiletimeasserts.cpp
    lib/core/barf_compiletimeasserts.hpp
    lib/core/barf_enums.cpp
    lib/core/barf_enums.hpp
    lib/core/barf_filoc.cpp
    lib/core/barf_filoc.hpp
    lib/core/barf_graph.cpp
    lib/core/barf_graph.hpp
    lib/core/barf_inputbase.cpp
    lib/core/barf_inputbase.hpp
    lib/core/barf_list.hpp
    lib/core/barf_message.cpp
    lib/core/barf_message.hpp
    lib/core/barf_optionsbase.cpp
    lib/core/barf_optionsbase.hpp
    lib/core/barf_pointer.hpp
    lib/core/barf_searchpath.cpp
    lib/core/barf_searchpath.hpp
    lib/core/barf_types.hpp
    lib/core/barf_util.cpp
    lib/core/barf_util.hpp
    lib/core/barf_weakreference.hpp
)

add_library(barf_core ${barf_core_SOURCES})
target_compile_definitions(barf_core PUBLIC PACKAGE_VERSION="${PROJECT_VERSION}" HARDCODED_BARF_TARGETS_DIR="${HARDCODED_BARF_TARGETS_DIR}")
target_include_directories(barf_core PUBLIC ${PROJECT_SOURCE_DIR}/lib/core)
target_link_libraries(barf_core PUBLIC Strict)

#
# barf_preprocessor
#

if(barf_DEVELOPER_MODE)
    reflex_add_source(${PROJECT_SOURCE_DIR}/lib/preprocessor/barf_preprocessor_scanner.reflex barf_preprocessor_scanner)
    trison_add_source(${PROJECT_SOURCE_DIR}/lib/preprocessor/barf_preprocessor_parser.trison barf_preprocessor_parser)
endif()

reflex_add_source_dev(${PROJECT_SOURCE_DIR}/lib/preprocessor/barf_preprocessor_scanner.reflex dev_barf_preprocessor_scanner)
trison_add_source_dev(${PROJECT_SOURCE_DIR}/lib/preprocessor/barf_preprocessor_parser.trison dev_barf_preprocessor_parser)

set(barf_preprocessor_SOURCES
    lib/preprocessor/barf_preprocessor.hpp
    lib/preprocessor/barf_preprocessor_ast.cpp
    lib/preprocessor/barf_preprocessor_ast_execute.cpp
    lib/preprocessor/barf_preprocessor_ast.hpp
    lib/preprocessor/barf_preprocessor_ast_print.cpp
    lib/preprocessor/barf_preprocessor_parser.cpp
    lib/preprocessor/barf_preprocessor_parser.hpp
    lib/preprocessor/barf_preprocessor_parser.trison
    lib/preprocessor/barf_preprocessor_scanner.cpp
    lib/preprocessor/barf_preprocessor_scanner.hpp
    lib/preprocessor/barf_preprocessor_scanner.reflex
    lib/preprocessor/barf_preprocessor_symboltable.cpp
    lib/preprocessor/barf_preprocessor_symboltable.hpp
    lib/preprocessor/barf_preprocessor_textifier.cpp
    lib/preprocessor/barf_preprocessor_textifier.hpp
)

add_library(barf_preprocessor ${barf_preprocessor_SOURCES})
target_compile_definitions(barf_preprocessor PUBLIC PACKAGE_VERSION="${PROJECT_VERSION}" HARDCODED_BARF_TARGETS_DIR="${HARDCODED_BARF_TARGETS_DIR}")
target_include_directories(barf_preprocessor PUBLIC ${PROJECT_SOURCE_DIR}/lib/preprocessor)
target_link_libraries(barf_preprocessor PUBLIC Strict barf_core)

#
# barf_regex
#

if(barf_DEVELOPER_MODE)
    trison_add_source(${PROJECT_SOURCE_DIR}/lib/regex/barf_regex_parser.trison barf_regex_parser)
endif()

trison_add_source_dev(${PROJECT_SOURCE_DIR}/lib/regex/barf_regex_parser.trison dev_barf_regex_parser)

set(barf_regex_SOURCES
    lib/regex/barf_regex.cpp
    lib/regex/barf_regex.hpp
    lib/regex/barf_regex_ast.cpp
    lib/regex/barf_regex_ast.hpp
    lib/regex/barf_regex_dfa.cpp
    lib/regex/barf_regex_dfa.hpp
    lib/regex/barf_regex_graph.cpp
    lib/regex/barf_regex_graph.hpp
    lib/regex/barf_regex_nfa.cpp
    lib/regex/barf_regex_nfa.hpp
    lib/regex/barf_regex_parser.cpp
    lib/regex/barf_regex_parser.hpp
    lib/regex/barf_regex_parser.trison
)

add_library(barf_regex ${barf_regex_SOURCES})
target_compile_definitions(barf_regex PUBLIC PACKAGE_VERSION="${PROJECT_VERSION}" HARDCODED_BARF_TARGETS_DIR="${HARDCODED_BARF_TARGETS_DIR}")
target_include_directories(barf_regex PUBLIC ${PROJECT_SOURCE_DIR}/lib/regex)
target_link_libraries(barf_regex PUBLIC Strict barf_core)

###############################################################################
# Executables
###############################################################################

#
# bpp (BARF Pre-Processor)
#

set(BPP_SOURCES
    app/bpp/bpp.hpp
    app/bpp/bpp_main.cpp
    app/bpp/bpp_options.cpp
    app/bpp/bpp_options.hpp
)
add_executable(bpp ${BPP_SOURCES})
target_compile_definitions(bpp PUBLIC PACKAGE_VERSION="${PROJECT_VERSION}" HARDCODED_BARF_TARGETS_DIR="${HARDCODED_BARF_TARGETS_DIR}")
target_include_directories(bpp PUBLIC ${PROJECT_SOURCE_DIR}/app/bpp)
target_link_libraries(bpp PUBLIC Strict barf_core barf_preprocessor)

#
# reflex (scanner generator)
#

if(barf_DEVELOPER_MODE)
    trison_add_source(${PROJECT_SOURCE_DIR}/app/reflex/reflex_parser.trison reflex_parser)
endif()

trison_add_source_dev(${PROJECT_SOURCE_DIR}/app/reflex/reflex_parser.trison dev_reflex_parser)

set(REFLEX_SOURCES
    app/reflex/reflex.hpp
    app/reflex/reflex_ast.cpp
    app/reflex/reflex_ast.hpp
    app/reflex/reflex_automaton.cpp
    app/reflex/reflex_automaton.hpp
    app/reflex/reflex_codespecsymbols.cpp
    app/reflex/reflex_codespecsymbols.hpp
    app/reflex/reflex_main.cpp
    app/reflex/reflex_options.cpp
    app/reflex/reflex_options.hpp
    app/reflex/reflex_parser.cpp
    app/reflex/reflex_parser.hpp
    app/reflex/reflex_parser.trison
)
add_executable(reflex ${REFLEX_SOURCES})
target_compile_definitions(reflex PUBLIC PACKAGE_VERSION="${PROJECT_VERSION}" HARDCODED_BARF_TARGETS_DIR="${HARDCODED_BARF_TARGETS_DIR}")
target_include_directories(reflex PUBLIC ${PROJECT_SOURCE_DIR}/app/reflex)
target_link_libraries(reflex PUBLIC Strict barf_commonlang_and_targetspec barf_core barf_preprocessor barf_regex)

#
# trison (parser generator)
#

if(barf_DEVELOPER_MODE)
    trison_add_source(${PROJECT_SOURCE_DIR}/app/trison/trison_parser.trison trison_parser)
endif()

trison_add_source_dev(${PROJECT_SOURCE_DIR}/app/trison/trison_parser.trison dev_trison_parser)

set(TRISON_SOURCES
    app/trison/trison.hpp
    app/trison/trison_ast.cpp
    app/trison/trison_ast.hpp
    app/trison/trison_codespecsymbols.cpp
    app/trison/trison_codespecsymbols.hpp
    app/trison/trison_dpda.cpp
    app/trison/trison_dpda.hpp
    app/trison/trison_enums.cpp
    app/trison/trison_enums.hpp
    app/trison/trison_graph.cpp
    app/trison/trison_graph.hpp
    app/trison/trison_main.cpp
    app/trison/trison_npda.cpp
    app/trison/trison_npda.hpp
    app/trison/trison_options.cpp
    app/trison/trison_options.hpp
    app/trison/trison_parser.cpp
    app/trison/trison_parser.hpp
    app/trison/trison_parser.trison
)
add_executable(trison ${TRISON_SOURCES})
target_compile_definitions(trison PUBLIC PACKAGE_VERSION="${PROJECT_VERSION}" HARDCODED_BARF_TARGETS_DIR="${HARDCODED_BARF_TARGETS_DIR}")
target_include_directories(trison PUBLIC ${PROJECT_SOURCE_DIR}/app/trison)
target_link_libraries(trison PUBLIC Strict barf_commonlang_and_targetspec barf_core barf_preprocessor)

###############################################################################
# Install rules
###############################################################################

install(
    TARGETS bpp reflex trison                   # Executables produced by this package
    RUNTIME DESTINATION bin                     # This is relative to CMAKE_INSTALL_PREFIX
)
install(
    DIRECTORY targets                           # This holds all the reflex and trison targets (targetspec and codespec files)
    DESTINATION lib/barf                        # This is relative to CMAKE_INSTALL_PREFIX
)
install(
    DIRECTORY cmake                             # This installs other cmake scripts.
    DESTINATION lib/barf                        # This is relative to CMAKE_INSTALL_PREFIX
)
install(
    DIRECTORY extra                             # This holds "extras" (e.g. syntax highlighting files)
    DESTINATION lib/barf                        # This is relative to CMAKE_INSTALL_PREFIX
)
install(
    FILES LICENSE README TODO barf-config.cmake # Important files; barf-config.cmake is how find_package(barf) works.
    DESTINATION lib/barf                        # This is relative to CMAKE_INSTALL_PREFIX
)
if(DEFINED DOXYGEN_EXECUTABLE)
    install(
        DIRECTORY ${PROJECT_BINARY_DIR}/doc     # Doxygen-generated documentation
        DESTINATION lib/barf                    # This is relative to CMAKE_INSTALL_PREFIX
    )
endif()

###############################################################################
# Helper targets (this defines a hierarchy of related build rules)
###############################################################################

if(barf_DEVELOPER_MODE)
    add_custom_target(all_parsers DEPENDS barf_preprocessor_parser barf_regex_parser barf_targetspec_parser reflex_parser trison_parser)
    add_custom_target(all_scanners DEPENDS barf_commonlang_scanner barf_preprocessor_scanner)
    add_custom_target(all_thingies DEPENDS all_parsers all_scanners)

    add_custom_target(force_all_parsers DEPENDS force_barf_preprocessor_parser force_barf_regex_parser force_barf_targetspec_parser force_reflex_parser trison_parser)
    add_custom_target(force_all_scanners DEPENDS force_barf_commonlang_scanner force_barf_preprocessor_scanner)
    add_custom_target(force_all_thingies DEPENDS force_all_parsers force_all_scanners)

    add_custom_target(clean_all_parsers DEPENDS clean_barf_preprocessor_parser clean_barf_regex_parser clean_barf_targetspec_parser clean_reflex_parser clean_trison_parser)
    add_custom_target(clean_all_scanners DEPENDS clean_barf_commonlang_scanner clean_barf_preprocessor_scanner)
    add_custom_target(clean_all_thingies DEPENDS clean_all_parsers clean_all_scanners)

    add_custom_target(all_parser_dot_pngs DEPENDS barf_preprocessor_parser_dot_png barf_regex_parser_dot_png barf_targetspec_parser_dot_png reflex_parser_dot_png trison_parser_dot_png)
    add_custom_target(all_parser_npda_pngs DEPENDS barf_preprocessor_parser_npda_png barf_regex_parser_npda_png barf_targetspec_parser_npda_png reflex_parser_npda_png trison_parser_npda_png)
    add_custom_target(all_scanner_dfa_pngs DEPENDS barf_commonlang_scanner_dfa_png barf_preprocessor_scanner_dfa_png)
    add_custom_target(all_scanner_nfa_pngs DEPENDS barf_commonlang_scanner_nfa_png barf_preprocessor_scanner_nfa_png)
    add_custom_target(all_scanner_dot_pngs DEPENDS barf_commonlang_scanner_dot_png barf_preprocessor_scanner_dot_png)
    add_custom_target(all_thingy_dot_pngs DEPENDS all_parser_dot_pngs all_scanner_dot_pngs)

    add_custom_target(clean_all_parser_dot_pngs DEPENDS clean_barf_preprocessor_parser_dot_png clean_barf_regex_parser_dot_png clean_barf_targetspec_parser_dot_png clean_reflex_parser_dot_png clean_trison_parser_dot_png)
    add_custom_target(clean_all_parser_npda_pngs DEPENDS clean_barf_preprocessor_parser_npda_png clean_barf_regex_parser_npda_png clean_barf_targetspec_parser_npda_png clean_reflex_parser_npda_png clean_trison_parser_npda_png)
    add_custom_target(clean_all_scanner_dfa_pngs DEPENDS clean_barf_commonlang_scanner_dfa_png clean_barf_preprocessor_scanner_dfa_png)
    add_custom_target(clean_all_scanner_nfa_pngs DEPENDS clean_barf_commonlang_scanner_nfa_png clean_barf_preprocessor_scanner_nfa_png)
    add_custom_target(clean_all_scanner_dot_pngs DEPENDS clean_barf_commonlang_scanner_dot_png clean_barf_preprocessor_scanner_dot_png)
    add_custom_target(clean_all_thingy_dot_pngs DEPENDS clean_all_parser_dot_pngs clean_all_scanner_dot_pngs)
endif()

add_custom_target(all_dev_parsers DEPENDS dev_barf_preprocessor_parser dev_barf_regex_parser dev_barf_targetspec_parser dev_reflex_parser dev_trison_parser)
add_custom_target(all_dev_scanners DEPENDS dev_barf_commonlang_scanner dev_barf_preprocessor_scanner)
add_custom_target(all_dev_thingies DEPENDS all_dev_parsers all_dev_scanners)

add_custom_target(diff_all_dev_parsers DEPENDS diff_dev_barf_preprocessor_parser diff_dev_barf_regex_parser diff_dev_barf_targetspec_parser diff_dev_reflex_parser diff_dev_trison_parser)
add_custom_target(diff_all_dev_scanners DEPENDS diff_dev_barf_commonlang_scanner diff_dev_barf_preprocessor_scanner)
add_custom_target(diff_all_dev_thingies DEPENDS diff_all_dev_parsers diff_all_dev_scanners)

add_custom_target(clean_all_dev_parsers DEPENDS clean_dev_barf_preprocessor_parser clean_dev_barf_regex_parser clean_dev_barf_targetspec_parser clean_dev_reflex_parser clean_dev_trison_parser)
add_custom_target(clean_all_dev_scanners DEPENDS clean_dev_barf_commonlang_scanner clean_dev_barf_preprocessor_scanner)
add_custom_target(clean_all_dev_thingies DEPENDS clean_all_dev_parsers clean_all_dev_scanners)

add_custom_target(all_dev_parser_dot_pngs DEPENDS dev_barf_preprocessor_parser_dot_png dev_barf_regex_parser_dot_png dev_barf_targetspec_parser_dot_png dev_reflex_parser_dot_png dev_trison_parser_dot_png)
add_custom_target(all_dev_parser_npda_pngs DEPENDS dev_barf_preprocessor_parser_npda_png dev_barf_regex_parser_npda_png dev_barf_targetspec_parser_npda_png dev_reflex_parser_npda_png dev_trison_parser_npda_png)
add_custom_target(all_dev_scanner_dfa_pngs DEPENDS dev_barf_commonlang_scanner_dfa_png dev_barf_preprocessor_scanner_dfa_png)
add_custom_target(all_dev_scanner_nfa_pngs DEPENDS dev_barf_commonlang_scanner_nfa_png dev_barf_preprocessor_scanner_nfa_png)
add_custom_target(all_dev_scanner_dot_pngs DEPENDS dev_barf_commonlang_scanner_dot_png dev_barf_preprocessor_scanner_dot_png)
add_custom_target(all_dev_thingy_dot_pngs DEPENDS all_dev_parser_dot_pngs all_dev_scanner_dot_pngs)

add_custom_target(clean_all_dev_parser_dot_pngs DEPENDS clean_dev_barf_preprocessor_parser_dot_png clean_dev_barf_regex_parser_dot_png clean_dev_barf_targetspec_parser_dot_png clean_dev_reflex_parser_dot_png clean_dev_trison_parser_dot_png)
add_custom_target(clean_all_dev_parser_npda_pngs DEPENDS clean_dev_barf_preprocessor_parser_npda_png clean_dev_barf_regex_parser_npda_png clean_dev_barf_targetspec_parser_npda_png clean_dev_reflex_parser_npda_png clean_dev_trison_parser_npda_png)
add_custom_target(clean_all_dev_scanner_dfa_pngs DEPENDS clean_dev_barf_commonlang_scanner_dfa_png clean_dev_barf_preprocessor_scanner_dfa_png)
add_custom_target(clean_all_dev_scanner_nfa_pngs DEPENDS clean_dev_barf_commonlang_scanner_nfa_png clean_dev_barf_preprocessor_scanner_nfa_png)
add_custom_target(clean_all_dev_scanner_dot_pngs DEPENDS clean_dev_barf_commonlang_scanner_dot_png clean_dev_barf_preprocessor_scanner_dot_png)
add_custom_target(clean_all_dev_thingy_dot_pngs DEPENDS clean_all_dev_parser_dot_pngs clean_all_dev_scanner_dot_pngs)

###############################################################################
# CPack rules for creating distributions
###############################################################################

# Reference: https://github.com/geoffmcl/cpack-test

if(WIN32)
    if(USE_WIX_TOOLSET)
        set(CPACK_GENERATOR "WIX") # this need WiX Tooset installed and a path to candle.exe
    else ()
        set(CPACK_GENERATOR "NSIS") # this needs NSIS installed, and available
    endif ()
    set(CPACK_SOURCE_GENERATOR "ZIP")
elseif(${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
    set(CPACK_GENERATOR "PackageMake")
else()
    set(CPACK_GENERATOR "DEB")
    set(CPACK_SOURCE_GENERATOR "TGZ")
endif()

set(CPACK_DEBIAN_PACKAGE_MAINTAINER "Victor Dods")

string(REPLACE "." ";" VERSION_LIST ${PROJECT_VERSION})
list(GET VERSION_LIST 0 barf_VERSION_MAJOR)
list(GET VERSION_LIST 1 barf_VERSION_MINOR)
list(GET VERSION_LIST 2 barf_VERSION_PATCH)

set(CPACK_PACKAGE_VERSION "${barf_VERSION_MAJOR}.${barf_VERSION_MINOR}.${barf_VERSION_PATCH}")
set(CPACK_PACKAGE_VERSION_MAJOR "${barf_VERSION_MAJOR}")
set(CPACK_PACKAGE_VERSION_MINOR "${barf_VERSION_MINOR}")
set(CPACK_PACKAGE_VERSION_PATCH "${barf_VERSION_PATCH}")

# set(CPACK_PACKAGE_DESCRIPTION_FILE "${PROJECT_SOURCE_DIR}/README.html")
set(CPACK_RESOURCE_FILE_LICENSE "${PROJECT_SOURCE_DIR}/LICENSE")
set(CPACK_RESOURCE_FILE_README "${PROJECT_SOURCE_DIR}/README")
# set(CPACK_RESOURCE_FILE_WELCOME "${PROJECT_SOURCE_DIR}/README.html")

set(CPACK_PACKAGE_FILE_NAME "${PROJECT_NAME}-${PROJECT_VERSION}")
set(CPACK_SOURCE_PACKAGE_FILE_NAME "${PROJECT_NAME}-${PROJECT_VERSION}")

set(CPACK_SOURCE_IGNORE_FILES "${PROJECT_SOURCE_DIR}/build/;${PROJECT_SOURCE_DIR}/.git/;${PROJECT_SOURCE_DIR}/.gitignore;${PROJECT_SOURCE_DIR}/barf.kdev4;${PROJECT_SOURCE_DIR}/.kdev4;${PROJECT_SOURCE_DIR}/dev/calc/build/;${PROJECT_SOURCE_DIR}/test/build/")

include(CPack)
